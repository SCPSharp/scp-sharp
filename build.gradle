import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'net.kyori.indra.license-header' version '2.1.1'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '1.2.1'
    id 'maven-publish'
}

group 'com.xtex'
version '1.1'

repositories {
    mavenCentral()
    maven { url 'https://maven.terraformersmc.com/releases/' } // modmenu
    maven { url 'https://maven.shedaniel.me' } // REI
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:1.7.1+kotlin.1.6.10"

    modImplementation 'com.terraformersmc:modmenu:3.0.1'
    modCompileOnly 'me.shedaniel:RoughlyEnoughItems-api-fabric:7.3.432'
    modRuntimeOnly 'me.shedaniel:RoughlyEnoughItems-fabric:7.3.432'
}

loom {
    accessWidenerPath.set file("src/main/resources/scpsharp.accesswidener")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

java {
    withSourcesJar()
}

jar {
    from('LICENSE') {
        rename { "${it}_${project.name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}

curseforge {
    if (System.getenv("CURSEFORGE_API_TOKEN") != null) {
        apiKey System.getenv("CURSEFORGE_API_TOKEN")
    } else if (project.hasProperty('curseforge_api_token')) {
        apiKey project.curseforge_api_token
    } else {
        apiKey ''
    }
    project {
        id = '583329'
        releaseType = 'release'
        changelog = 'See GitHub for changelog'
        addGameVersion 'Fabric'
        addGameVersion 'Java 17'
        addGameVersion project.minecraft_version

        mainArtifact(remapJar) {
            displayName = "mc${project.minecraft_version}-${project.version}"
        }

        afterEvaluate {
            uploadTask.dependsOn remapJar
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

task modrinth(type: TaskModrinthUpload, dependsOn: remapJar) {
    onlyIf {
        System.getenv("MODRINTH_TOKEN") != null
    }

    projectId = 'p3O2a1Bc'
    versionNumber = version
    versionName = "mc${project.minecraft_version}-$project.version"
    changelog = 'See GitHub for changelog'

    uploadFile = remapJar

    addGameVersion project.minecraft_version
    addLoader 'fabric'
}
